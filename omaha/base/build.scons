# Copyright 2009 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ========================================================================

Import('env')

import glob
import operator
import os
import os.path

env.BuildSConscript('logging')
env.BuildSConscript('security')

local_env = env.Clone()

inputs = [
    'apply_tag.cc',
    'app_util.cc',
    'browser_utils.cc',
    'cgi.cc',
    'clipboard.cc',
    'command_line_parser.cc',
    'command_line_validator.cc',
    'commands.cc',
    'crash_if_specific_error.cc',
    'debug.cc',
    'disk.cc',
    'dynamic_link_kernel32.cc',
    'encrypt.cc',
    'environment_block_modifier.cc',
    'environment_utils.cc',
    'error.cc',
    'event_trace_controller.cc',
    'event_trace_provider.cc',
    'etw_log_writer.cc',
    'extractor.cc',
    'file.cc',
    'file_reader.cc',
    'file_ver.cc',
    'firewall_product_detection.cc',
    'highres_timer-win32.cc',
    'logging.cc',
    'omaha_version.cc',
    'path.cc',
    'process.cc',
    'proc_utils.cc',
    'program_instance.cc',
    'queue_timer.cc',
    'reactor.cc',
    'reg_key.cc',
    'registry_monitor_manager.cc',
    'safe_format.cc',
    'service_utils.cc',
    'shell.cc',
    'shutdown_handler.cc',
    'signatures.cc',
    'signaturevalidator.cc',
    'string.cc',
    'synchronized.cc',
    'system.cc',
    'system_info.cc',
    'thread.cc',
    'thread_pool.cc',
    'time.cc',
    'timer.cc',
    'user_info.cc',
    'user_rights.cc',
    'utils.cc',
    'vista_utils.cc',
    'vistautil.cc',
    'window_utils.cc',
    'wmi_query.cc',
    'xml_utils.cc',

    '../third_party/chrome/files/src/base/cpu.cc',
    '../third_party/chrome/files/src/base/rand_util.cc',
    '../third_party/chrome/files/src/crypto/rsa_private_key.cc',
    '../third_party/chrome/files/src/crypto/rsa_private_key_win.cc',
    '../third_party/chrome/files/src/crypto/signature_creator_win.cc',
    ]

# Required by the exception barrier code.
local_env.Append(ASFLAGS = ['/safeseh'])

# Build these into a library.
local_env.ComponentStaticLibraryMultiarch('base', inputs)

if 'OMAHA_PROTOBUF_BIN_DIR' not in os.environ:
  """ Build the Protocol Buffer Compiler executable protoc.exe."""

  def GenerateGYPRunAction(source, target, env, for_signature):
    """This Generator creates a command line that when executed runs gyp on the
       specified source file to generate the specified target file.

       * We explicitly change to the directory of the
       source file before running gyp. Not doing this confuses some of the gyp
       generators, especially the msvs generator.
       * The |--generator-output| option is needed to generate the target in the
       target directory. Without |--generator-output|, the target is generated
       in the source directory, which is not desirable especially if the source
       directory is read-only. Explicitly generating the target in a different
       output directory also avoids other warnings.
       * |--generator-output| needs to be specified as a
       path relative to the source file path to be compatible with the gyp
       generators.

    Args:
      source: A List containing a single .gyp file path.
      target: A List containing a single target file path.
      env: The Environment in which to build.
      for_signature: We ignore this parameter. It indicates to the generator to
                     just generate the command and not actually run it.

    Returns:
      A valid command line that when executed runs gyp on the specified source
      file to generate the specified target file.
    """

    # We use the googleclient gyp since the google3 gyp does not work well with
    # python 2.7.
    gyp_bat_file = '$GOOGLECLIENT\\third_party\\gyp\\files\\gyp.bat'

    # Explicitly using 'cd', since using the chdir option to env.Command() is
    # not compatible with parallel builds using the -j option.
    gyprun_action = 'cd %s' % source[0].dir.abspath
    gyprun_action += ' && '  # Chaining the gyp generation to the 'cd' command.
    gyprun_action += '%s %s --generator-output=%s -G msvs_version=2005' % (
        env.File(gyp_bat_file).abspath,
        source[0].name,
        os.path.relpath(target[0].dir.abspath, source[0].dir.abspath))

    return gyprun_action

  Import('env')
  local_env = env.Clone()

  # Use gyp to generate protoc.sln, which in turn is fed to VCBuild to generate
  # protoc.exe.
  # gyp.
  gyprun = Builder(generator = GenerateGYPRunAction)
  local_env.Append( BUILDERS = {'GYPRun' : gyprun})
  protoc_sln = local_env.GYPRun(
    target='protoc.sln',
    source='$GOOGLE3/net/proto2/contrib/portable/gyp/protoc.gyp')

  # VCBuild.
  vcbld = Builder(action =
      '$GOOGLECLIENT\\third_party\\vc_80\\files\\vc\\vcpackages\\vcbuild.exe' +
      ' /useenv $SOURCE "Default|Win32"')
  local_env.Append( BUILDERS = {'VCBuild' : vcbld})
  local_env.VCBuild(target='Default/protoc.exe', source=protoc_sln)

""" Build libprotobuf. """

default_protobuf_src_dir = '$GOOGLE3/third_party/protobuf/src'
protobuf_src_dir = os.getenv('OMAHA_PROTOBUF_SRC_DIR', default_protobuf_src_dir)

proto_env = env.Clone()
protobuf_src_path = os.path.join(protobuf_src_dir, 'google/protobuf/')
proto_env.Append(
    CPPDEFINES=['LIBPROTOBUF_EXPORTS'],
    CCFLAGS=[
        '/wd4005',
        '/wd4018',
        '/wd4065',
        '/wd4100',
        '/wd4125',
        '/wd4146',
        '/wd4242',
        '/wd4244',
        '/wd4267',
        '/wd4309',
        '/wd4310',
        '/wd4355',
        '/wd4388',
        '/wd4389',
        '/wd4456',
        '/wd4506',
        '/wd4548',
        '/wd4647',
        '/wd4701',
        '/wd4702',
        '/wd4703',
        '/wd4715',
        '/wd4798',
        '/wd4800',
        '/wd4946',
    ],
    CPPPATH=[
        '$GOOGLE3',
        protobuf_src_dir,
        protobuf_src_path,
    ],
)

protobuf_src_path_dir = proto_env.Dir(protobuf_src_path)
proto_env.Dir('protobuf').addRepository(protobuf_src_path_dir)
cc_files = [
    'protobuf/compiler/importer.cc',
    'protobuf/compiler/parser.cc',
    'protobuf/any.cc',
    'protobuf/descriptor.cc',
    'protobuf/descriptor.pb.cc',
    'protobuf/descriptor_database.cc',
    'protobuf/dynamic_message.cc',
    'protobuf/extension_set.cc',
    'protobuf/extension_set_heavy.cc',
    'protobuf/generated_message_reflection.cc',
    'protobuf/generated_message_util.cc',
    'protobuf/implicit_weak_message.cc',
    'protobuf/io/coded_stream.cc',
    'protobuf/io/io_win32.cc',
    'protobuf/io/printer.cc',
    'protobuf/io/strtod.cc',
    'protobuf/io/tokenizer.cc',
    'protobuf/io/strtod.cc',
    'protobuf/io/zero_copy_stream.cc',
    'protobuf/io/zero_copy_stream_impl.cc',
    'protobuf/io/zero_copy_stream_impl_lite.cc',
    'protobuf/arena.cc',
    'protobuf/map_field.cc',
    'protobuf/message.cc',
    'protobuf/message_lite.cc',
    'protobuf/parse_context.cc',
    'protobuf/reflection_ops.cc',
    'protobuf/repeated_field.cc',
    'protobuf/service.cc',
    'protobuf/stubs/common.cc',
    'protobuf/stubs/int128.cc',
    'protobuf/stubs/status.cc',
    'protobuf/stubs/stringprintf.cc',
    'protobuf/stubs/stringpiece.cc',
    'protobuf/stubs/structurally_valid.cc',
    'protobuf/stubs/strutil.cc',
    'protobuf/stubs/substitute.cc',
    'protobuf/text_format.cc',
    'protobuf/unknown_field_set.cc',
    'protobuf/wire_format.cc',
    'protobuf/wire_format_lite.cc',
]

proto_env.ComponentStaticLibraryMultiarch(
    'libprotobuf', cc_files, COMPONENT_STATIC=True)

""" Build the CRX Verifier libraries."""

Import('env')
local_env = env.Clone()

crx_file_path = '/third_party/chrome/files/src/components/crx_file/'
local_env.Append(PROTO_PATH = '$MAIN_DIR' + crx_file_path)
local_env.Append(CPP_OUT = '$TARGET_ROOT/proto_files' + crx_file_path)
proto_sources = [
    '$MAIN_DIR' + crx_file_path + 'crx3.proto'
]
cc_files = local_env.CompileProtoBuf(proto_sources)
local_env.Append(
    CPPPATH=[
        '$GOOGLE3',
        protobuf_src_dir,
        '$TARGET_ROOT/proto_files/',
    ],
)

cc_files += [
    '../third_party/chrome/files/src/components/crx_file/crx_verifier.cc',
    '../third_party/chrome/files/src/components/crx_file/id_util.cc',
    '../third_party/chrome/files/src/crypto/signature_verifier.cc',
    '../third_party/chrome/files/src/crypto/signature_verifier_win.cc',
    '../third_party/chrome/files/src/crypto/secure_util.cc',
]

""" Add zlib library files."""
zlib_src_path = '$THIRD_PARTY/zlib/v1_2_11/'
zlib_src_path_dir = local_env.Dir(zlib_src_path)
local_env.Dir('zlib').addRepository(zlib_src_path_dir)
cc_files += ['zlib/' +
             os.path.basename(x) for x in
             glob.glob(zlib_src_path_dir.path + os.sep + '*.c')]

""" Add libzip library files."""

libzip_src_path = '$THIRD_PARTY/libzip/lib/'
libzip_src_path_dir = local_env.Dir(libzip_src_path)
local_env.Dir('libzip').addRepository(libzip_src_path_dir)
libzip_gladman_src_path = '$THIRD_PARTY/libzip/lib/gladman-fcrypt'

local_env.Append(
    CPPDEFINES=[
        'HAVE_CONFIG_H',
    ],
    CPPPATH=[
        'libzip/',
        libzip_gladman_src_path,
        libzip_src_path,
        zlib_src_path,
    ],
    CCFLAGS=[
        '/wd4005',
        '/wd4100',
        '/wd4013',
        '/wd4047',
        '/wd4131',
        '/wd4132',
        '/wd4242',
        '/wd4244',
        '/wd4245',
        '/wd4255',
        '/wd4267',
        '/wd4310',
        '/wd4312',
        '/wd4388',
        '/wd4389',
        '/wd4647',
        '/wd4548',
    ],
)

cc_files_exclusions = [
    '*unix*.c',
    'zip_crypto_[cgmo]*.c',
    'zip_source_file.c',
    'zip_random_uwp.c',
    'zip_winzip_aes.c',
    '*bzip2.c',
    'zip_source_winzip_aes_*.c'
]
cc_files += ['libzip/' + os.path.basename(x) for x in list(
    set(glob.glob(os.sep.join([libzip_src_path_dir.path, '*.c']))) -
    set(reduce(operator.concat, map (lambda x : glob.glob(
      os.sep.join([libzip_src_path_dir.path, x])), cc_files_exclusions))))]

local_env.ComponentStaticLibraryMultiarch(
    'crx_file', cc_files, COMPONENT_STATIC=True)
