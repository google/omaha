#!/usr/bin/python2.4
#
# Copyright 2011 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ========================================================================

""" Build enterprise installer test.
"""

Import('env')

from standalone import standalone_installer

# Build test_setup.exe
test_setup_env = env.Clone()

test_setup_env.Append(
    LIBS = [
        test_setup_env['crt_libs'][test_setup_env.Bit('debug')],
        ],
)

test_setup_env.FilterOut(
    CPPDEFINES=['_WINDOWS'],
    LINKFLAGS=['/SUBSYSTEM:WINDOWS'],
)
test_setup_env.Append(
    CPPDEFINES=['_CONSOLE'],
    LINKFLAGS=['/SUBSYSTEM:CONSOLE'],
)

target_name = 'test_setup'

exe_inputs = [
    'test_setup.cc',
    ]

test_setup_env.ComponentProgram(
    prog_name = target_name,
    source = exe_inputs,
)

# Build standalone and enterprise installers for test_setup using each version.
for omaha_version_info in env['omaha_versions_info']:
  prefix = omaha_version_info.filename_prefix

  source_binary = '$OBJ_ROOT/mi_exe_stub/%smi_exe_stub.exe' % (prefix)

  standalone_installer.BuildOfflineInstallersVersion(
      env,
      omaha_version_info,
      '$STAGING_DIR',
      source_binary,
      '$MAIN_DIR/enterprise/installer/test/standalone_installers.txt',
      '$MAIN_DIR/enterprise/installer/test',
      prefix)
